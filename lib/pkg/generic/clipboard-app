#!/usr/bin/zsh

MULTY_VERSION_PACKAGE[clipboard-app]="YES"
CLIPBOARD_KEEP_EXISTING=1

system_pkg_action_multy_clipboard_app_common(){
	local package=$1
	local store=$2

	clibpoard_keep_existing $store && return 1

	local selection
	local menu_prompt=""

	menu_prompt+="[${g}ENTER: ${n}select] "
	menu_prompt+="[${y}CTRL+C: ${n}quit]"

	local packages=(
		"xclip;cliboard support on X11"
		"xsel;cliboard support on X11"
		"wl-clipboard;cliboard support on Wayland"
	)

	local i

	for i in {1..$#packages}; do
		local pkg=${packages[$i]%;*}
		local inst_version=$(zpackage system version $pkg installed)
		local pkg_status=""

		[[ -n $inst_version ]] && [[ $inst_version != 0.0 ]] && pkg_status="(installed)"

		packages[$i]="${packages[i]};$pkg_status"
	done

	packages+=("")
	packages+=("None")

	alert_msg "${y}Select cliboard support package"

	zmenu select selection\
		--width 60 \
		--header "Pkg;Description;Status" \
		--selected-color "b" \
		--focus 1 \
		--color 'g y n r' \
		--align "r l l l" \
		--spaces "1 2 2 2" \
		--no-bottom-line \
		--prompt "$menu_prompt" \
		"${(@)packages}" || {eval "$store=skipp"; return}

	echo

	local selected=${packages[$selection]%%;*}

	if [[ $selected = None ]]; then
		eval "$store=skipp"
		return
	fi

	local inst_version=$(zpackage system version $selected installed)
	local repo_version=$(zpackage system version $selected repo)

	local action=$(
		pkg_action_define $selected 0.0 $repo_version $inst_version
	)

	eval "$store=$action"
}

clibpoard_keep_existing(){
	local store=$1

	[[ $CLIPBOARD_KEEP_EXISTING -eq 0 ]] && return 1

	local clibpoards=(xclip xsel wl-clipboard)
	local inst_version
	local clip

	alert_msg "Checking [system]" "clipboard-app"
	echo

	for clip in $clibpoards; do
		local inst_version=$(zpackage system version $clip installed)

		[[ $inst_version != 0.0 ]] && break
	done

	local result=1

	if [[ -n $inst_version ]] && [[ $inst_version != 0.0 ]]; then
		local repo_version=$(zpackage system version $clip repo)

		local action=$(
			pkg_action_define $clip 0.0 $repo_version $inst_version
		)

		eval "$store=$action"

		result=0
	fi

	clear_screen 5

	return $result
}

